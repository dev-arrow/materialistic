apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

task jacocoTestReport(type: JacocoReport) {
    reports {
        xml.enabled true
        csv.enabled false
        xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        html.destination "${buildDir}/reports/coverage"
    }

    classDirectories = fileTree(
            dir: "${buildDir}/intermediates/classes/debug",
            exclude: [
                    '**/R.class',
                    '**/R$*.class',
                    '**/*$InjectAdapter.class',
                    '**/*$ModuleAdapter.class',
                    '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files android.sourceSets.main.java.srcDirs
    executionData = files "${buildDir}/jacoco/testDebug.exec"

    // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174.
    // We iterate through the compiled .class tree and rename $$ to $.
    doFirst {
        new File("${buildDir}/intermediates/classes/debug").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }

    doLast {
        println "coveralls report has been generated to file://${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        println "jacoco report has been generated to file://${reports.html.destination}/index.html"
    }
}
