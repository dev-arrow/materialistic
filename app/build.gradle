plugins {
    id "me.tatarka.retrolambda" version "3.2.5"
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "io.github.hidroh.materialistic"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 52
        versionName "2.8"
        buildConfigField "int", "LATEST_RELEASE", "51"
        buildConfigField "String", "GITHUB_TOKEN", "\"\""
        buildConfigField "String", "READABILITY_TOKEN", "\"\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-square.pro',
                    'proguard-support.pro'
        }
        leak.initWith(buildTypes.debug)
    }

    lintOptions {
        htmlReport false
        xmlReport false
        textReport true
        warningsAsErrors true
        abortOnError true
        ignore 'ResourceType' // TODO http://b.android.com/201882
        informational 'Overdraw', // TODO http://b.android.com/169879
                'MissingTranslation'
        explainIssues false
        absolutePaths false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    supportVersion = '23.4.0'
    okHttpVersion = '3.3.1'
    daggerVersion = '1.2.2'
    leakCanaryVersion = '1.4-beta2'
    retrofitVersion = '2.0.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$supportVersion",
            "com.android.support:recyclerview-v7:$supportVersion",
            "com.android.support:cardview-v7:$supportVersion",
            "com.android.support:design:$supportVersion",
            "com.android.support:preference-v7:$supportVersion",
            "com.android.support:customtabs:$supportVersion",
            "com.squareup.dagger:dagger:$daggerVersion",
            "com.squareup.retrofit2:retrofit:$retrofitVersion",
            "com.squareup.retrofit2:converter-gson:$retrofitVersion",
            "com.squareup.okhttp3:okhttp:$okHttpVersion",
            "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    leakCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    debugCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    provided "com.squareup.dagger:dagger-compiler:$daggerVersion"
}

apply from: "${rootProject.getRootDir()}/robolectric.gradle"
apply from: "${rootProject.getRootDir()}/jacoco.gradle"
